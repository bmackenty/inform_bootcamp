"Yoke" by Bill MacKenty

[===== EXAMINE rules ======]
	a thing can be examined or unexamined. 
	a thing is usually unexamined.
	
	carry out examining something:
		now the noun is examined. 

[===== TALKING rules ======]
talking to is an action applying to one visible thing. 
understand "talk to [someone]" and "speak to [someone]" and "converse with [someone]" as talking to.  
Check talking to [something]:
	if noun is not a person:
		say "You attempt to talk with the [noun]. Perhaps you can find someone who can talk back?";
		stop the action.
Carry out talking to:
	if noun is proper-named:
		say "You chat away with [noun] for a while.";
	else:
		say "You chat with the [noun] for a while.".
		

[===== TAKING rules ======]

understand "pick [something]" and "grab [something]" as taking.


[===== DRINKING rules ======]

a thing can be undrinkable or drinkable.
a thing is usually undrinkable. 

The block drinking rule is not listed in the check drinking rulebook. 

Check drinking:
	if noun is undrinkable:
		say "That doesn't seem to be something you can imbibe!";
		stop the action.

Carry out drinking:
	now noun is nowhere;
	say "you drink [noun].".
		
report drinking:
	say "Refreshing and satisfying!".

[===== PRAYING rules ======]

praying is an action applying to nothing. 
understand "pray" and "worship" as praying.
check praying:
	if player is in insideOldChurch:
		say "appropriate and peaceful.";
		stop the action;
		
carry out praying:
	say "Nothing happens".

[===== XYZZY rules ======]

xyzzy is an action applying to nothing.
understand "xyzzy" as xyzzy.
check xyzzy:
	if player is in insidePasture:
		say "Gentle and comforting swirling blue lights surround you. The lights move like a rotating helix around your body.  As your feet leave the ground, you feel a distinct warmth around you, which becomes warmer. The lights become very bright until you can no longer open your eyes. You feel a heavy weight upon your neck and a stern, harsh voice says 'the yoke might be easy, the burden might be light, but it is still a yoke and burden...'.
		
		You are unsure how much time has passed, but you can not help but notice you have changed into an ox. ";
		end the story finally saying "
		
		
		
		Be aware of the yokes you choose to wear. Not all are bad, not all burdens are necessary. 
		
		
		
		
		
		
		".
		
	
carry out xyzzy: 
	say "Nothing happens.".


[===== Initial inventory ======]


The player is wearing comfortable denim jeans.
The player is wearing a comfortable old linen shirt.
The player is wearing a pair of sturdy boots.
The player is wearing a straw hat.


[========== open field ==========]
An Open Field is a room. 
	An Old road is north of Open Field. 
	A outsidePasture is east of Open Field. 
	wildflower is scenery in Open Field.
	understand "flower" and "flowers" and "wild" and "wildflowers" as wildflower.

instead of examining wildflower:
	if wildflower is unexamined:
		say  "Among the beautiful assortment of wildflowers, you spot Blue Wild Indigo (Baptisia australis),  Sweet Joe Pye Weed (Eutrochium purpureum), and Pale Purple Coneflower (Echinacea pallida). There are other wildflowers and grasses of course, but those are the one's you can easily identify.";
		now the wildflower is examined;
		stop the action;
	else:
		say "The wildflowers are beautiful. It is better to let them freely grow than be picked. Wildflowers are an import part of the natural ecosystem.".
	
dirtRoad is scenery in the open field. 
understand "road" as dirtRoad.
the printed name of dirtRoad is "old dirt road".
instead of examining dirtRoad:
	if dirtRoad is unexamined:
		say "To the north you see an old dirt road. It has a strip of grass running down the middle. You think see some trees far in the distance to the north.";
		now the dirtRoad is examined;
		stop the action;
	else:
		say "The dirt road remains a dirt road, doing it's job as a dirt road. It was probably made many years ago.".

The description of Open Field is "[if unvisited]You stand in a large grassy field dotted with wildflowers commonly found in rural New England during the summer. There is an old country road meandering to the north. To the east near the crest of a hill some animals peacefully graze within the confines of a pasture.
[else]A familiar field with an old country road to the north and a pasture to the east.
". 
Instead of going nowhere in Open Field:
	say "Although large and beautiful, there may be other things worth seeing here...".
Instead of eating wildflower:
	say "You attempt to chew and eat the wildflowers, however, since you are not a cow, your efforts are largely unsuccessful.";
	now the description of the player is "Grass is stuck to and sticking out of many of your teeth. The effect is you now look like a horse / cow / farm animal with unbelievable  dental deficiency.".
instead of taking wildflower:
	say "It is better to let them freely grow than be picked. Wildflowers are an import part of the natural ecosystem.". 
	

[========== pasture ==========]
The description of outsidePasture is "[if unvisited]You stand outside a large pasture ringed by an old split-rail wooden fence. There are several large oxen in the pasture - grazing, using their tails to shoo away flies, or simply laying on the ground. 

To the west you can see an open bucolic field. You can enter the pasture. [else]Returning to the pasture, you see several oxen lazily grazing. You can enter the pasture.[end if]

	[if insidePasture is visited]The oxen, wary and suspicious of your presence after having entered their pasture, continue to carefully monitor your movements and actions. They look at you and keenly observe your movements and actions. 
	[else]The oxen become curious and casually alert to your arrival. Some of them stop grazing and stare at you, while others adjust their ears to better detect the sounds you make. ".

the printed name of outsidePasture is "Outside the pasture".

[ ============== fence ========]

fence is scenery in outsidePasture.
the printed name of fence is "old wooden split-rail fence".
understand "split rail fence" and "wooden fence" and "old fence"  and "fence" as fence.
the description of fence is "The old wooden split-rail fence is in fair condition. There are no obvious missing sections, but it looks like it might only last another season. Remarkable is the size of the fence; it creates a huge pasture capable of comfortably holding many hundreds of farm animals. Currently, there are only oxen in the pasture.".

instead of going nowhere in outsidePasture:
	say "The fence blocks your path.".
	
	[---------- old bench ------------]
	An old large bench is a supporter in the outsidePasture. "A large weathered workbench stands outside the fence. A pile of rusty old tools and a large wooden crate rests atop the workbench"
	
	The description of the old large bench is "The old workbench looks to have once been used to hold tools and equipment to work on the massive fence which surrounds the pasture. There is a large wooden crate on top of the workbench and many rusty old tools.".

	a key unlocks the large wooden crate.
	The description of key is "A brass key with a large paper tag attached; it reads 'for the crate'.".
	tools are scenery container on old bench. 
	Understand "tools" and "rusty tools" and "old tools" and "useless tools" as tools.
	instead of examining tools for the first time:
		say "There is large pile of rusty carpentry tools. Hammers, saws, pliers, squares, and files - all rusty and useless. As you look through the large pile of rusty useless tools you realize it certainly isn't worth looking through the tools again.";
		stop the action. 
		
	instead of examining the tools for the second time:
		say "Not much has changed since you first looked through the tools. Although it is fairly hard to read the emotional cues from oxen,  you are pretty sure they are relieved you have finally finished searching through the tools. Just like when you first looked through the tools, it doesn't seem like it is worth looking through the tools again.";
		stop the action.
		
	instead of examining the tools for the third time:
		say "As you poke around the rusty tools, you can't help but notice an elegantly made key. You've never seen a key like this before, and can't image what sort of lock it might fit. ";
		now the key is in the tools;
		stop the action.
		

	[-- yoke --]	
	A yoke is in the large wooden crate.
	A yoke is wearable.
	The description of the yoke is "A long piece of heavy wood with two identations where it fits onto the back of the oxens neck. The yoke is of exquisite construction, with delicate and elegant carvings in the wood. There are two large u-shaped pieces of wood which fit under an oxen neck and connect to the heavy yoke. They are also made of an exquisite quality. Finally, there is a metal ring set in the bottom-middle of the yoke. It looks to be made of brass with even more intricate carvings in the metal.".
	
	[-- old crate --]
	A large wooden crate is an closed openable locked scenery container on old large bench. 
	A large wooden crate is fixed in place. 
		
	The description of large wooden crate is "The crate is of a different construction from the workbench and fence; it seems to be more ornate and of higher quality than the workbench. It has not suffered the damage from time, neglect and weather which the bench and fence have. There is a strong, exquisitely made lock on the crate.".
	

[=== inside pasture ===]
insidePasture is inside from outsidePasture. 
The description of insidePasture is "You are standing inside a large open pasture. An old wooden split-rail fence defines the boundary of the pasture. 

The oxen, curiousity piqued by your arrival outside of the pasture, take a decidedly more active interest in your presence inside the pasture. Some of the oxen stand up, others lift their noses to better smell you, and a few of them take a few tentative steps towards you. A few of the oxen think it better to take a few steps away from you.  All of the oxen are looking at you.

You can exit the pasture.".


fauxInsidePasture is a scenery, enterable container in outsidePasture. 
the printed name of fauxInsidePasture is "Pasture".
the printed name of insidePasture is "Pasture".

understand "pasture" as fauxInsidePasture.

instead of entering fauxInsidePasture:
	try going inside.

inFence is scenery in insidePasture.
the printed name of inFence is "old wooden split-rail fence".
understand "split rail fence" and "wooden fence" and "old fence" and "fence" as inFence.
the description of inFence is "The old wooden split-rail fence is in fair condition. There are no obvious missing sections, but it looks like it might only last another season. Remarkable is the size of the fence; it creates a huge pasture capable of comfortably holding many hundreds of farm animals. Currently, there are only oxen (and you) inside the pasture.".

An ox is in the insidePasture.
An ox is scenery.
the description of ox is "This adult ox is massive; easily in excess of 1000 kilograms. It looks attentively at you as it slowly chews.".


instead of taking ox:
	end the story finally saying "As you attempt to pick up the ox, you learn a painful, final and important lesson about the personal boundaries of an ox.
	
		
	As if we need to discuss this further, you have died.  I am afraid this wasn't an especially peaceful and gentle parting, but rather the complete opposite. 
	
	
	Entire books might be written about how un-peaceful and rather unpleasant your last few moments were.
	
	 
	".

[========== old road ==========]

The printed name of old road is "Dusty old country road".
The description of old road is "You stand on an old dusty country road. A swatch of grass defines the median along the middle of the road. There is a very large and old linden tree here. Its branches and foliage providing comforting shade. 

To the south you can see an open bucolic field, and to the north an old decrepit wooden church.".
instead of going nowhere in old road:
	say "Although tempting to meander off, you think it better to stay on the road.".
An oldChurch is north of old road.


[=========== tree ============]
A tree is in old road.
A tree is scenery. 


The description of the tree is  "This ancient tree is massive. It's branches extend broadly and fully, creating a comforting shade in the warm sun. A tree of this age is usually granted a name, but you are not sure what it might be. ".

instead of climbing the tree:
	say "The tree is too difficult to climb.".


[========== old church ==========]

The printed name of oldChurch is "Outside of a small old decrepit wooden country church".
The description of oldChurch is "Peeling and fading paint falls from the walls of this church which has clearly seen better days. The grass around the church is closely trimmed, and the windows seem clean. The steeple of the church looks ready to fall, and a rusty Christian cross sits atop the spire. 

You can go in to the church from here.".

instead of going nowhere in oldChurch:
	say "There are no obvious pathways save for the old road to the south, and the entrance to the church.".

[======== in old church =======]
insideOldChurch is inside of oldChurch.
The printed name of insideOldChurch is "Inside the Church of the Yoke".
The description of insideOldChurch is "You are standing in a one room church. It is remarkably clean (considering the delapidated condition of the exterior). There are seven rows of wooden pews on each side of the church. There is a wooden altar in the front with a simple wooden cross afixed the wall behind. There is a yoke carved in the front of the altar, under which is carved the words 'my yoke is easy and my burden is light'. 

You can leave the church by going OUT. ".

understand "pews" as pew.
pew is in insideOldChurch.
pew is scenery.
The description of pew is "Long wooden benches for sitting during a church service. The pews look to hold about 6 adults each. ".

understand "yoke" as carving.
carving is inside insideOldChurch.
carving is scenery.
The description of carving is "Carved into the front of the altar is a picture of a yoke".


A table is a supporter in insideoldChurch. "A table stands near the entrance of the church. There is a wooden bowl on the table, and a glass of lemonade. A hand-written sign reads 'please feel free to eat the fruit and drink the water'.".
The description of table is "A wooden table. it looks rather old but well-cared for.".
a bowl is an open scenery container on the table.
The printed name of bowl is "simple hand-carved wooden bowl".
fruit is in the bowl.
fruit is edible.
the printed name of fruit is "fresh fruit".
the description of fruit is  "Fresh strawberries, blueberries, green grapes, melon and oranges are mixed together in a sweet and refreshing fruit salad.".


a glass is an undescribed thing  on the table.
the printed name of glass is "drinking glass".  
refreshingLemonade is part of glass.
the printed name of refreshingLemonade is "refreshing lemonade.".
refreshingLemonade is drinkable. 
understand "lemonade" as refreshingLemonade.

	
instead of taking bowl:
	say "The bowl belongs to the church and should probably not be taken.".
	
instead of taking table:
	say "The table belongs to the church and should probably not be taken.".
	
instead of eating fruit:
	say "Delicious! You can't recall the last time you tasted such delicious fruit.".

[----priest npc code here---]


fatherDrigi is a man in oldChurch. 
the printed name of fatherDrigi is "Father Drigi".  
fatherDrigi is proper-named.
understand "man" and "father drigi" and "priest" and "father" and "drigi" and "guy" and "pastor" as fatherDrigi.

instead of examining fatherDrigi:
	if unexamined:
		say "A tall, thin, expressionless priest. His movements and facial structure betray a rigidness in his character. From behind a pair of round glasses, he looks down his nose at you.
		
		You could try talking to him.";
		now fatherDrigi is examined;
		stop the action;
	else:
		say "Not much has changed about Father Drigi since you last looked at him.". 

instead of talking to fatherDrigi:
	say "Father Drigi doesn't seem interested in chit chat. Perhaps if you asked him something specific he might reply.".

instead of giving glass to fatherDrigi: 
	say "Father Drigi sniffs and curtly says 'I certainly don't need that glass! If you don't want it leave it in the church for a good Christian!'.";
	stop the action.

instead of giving something to fatherDrigi:
	say "Father Drigi arches an eyebrow and dismissively waves his hand. He curtly says 'I dont need that.' and looks away.";
	stop the action.
